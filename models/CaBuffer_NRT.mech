% # CaBuffer_NRT: Calcium buffer concentration dynamics, for generic
%     thalamic reticular cells used in the DynaSim implementation of (Krishnan
%     et al., 2016). Note that in the original code, this celltype is called
%     RE, and this mechanism is identified as ICa, even though it is not a
%     current.
%
% - Source: This was built from the original paper's code, using "currents.h"
%     lines 339-340, 1119 and "currents.cpp" lines 133-139, 1029.
%
% - References:
%     - Krishnan GP, Chauvette S, Shamie I, Soltani S, Timofeev I, Cash SS, et
%         al. Cellular and neurochemical basis of sleep stages in the
%         thalamocortical network. eLife. 2016;5: e18607
%
% - Requires: "@IT_NRT" exposed by "iT_NRT" mechanism (which is,
%     confusingly, called "IT_RE", NOT ICa, in the original code).
%
% - Exposes: calcium concentration "@CaBuffer_NRT" required for
%     "iT_NRT" mechanism
%
% - Tags: calcium, intrinsic, thalamus, reticular
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Parameters
tauR = 5         % ms
Ca_inf = 0.00024 % mM of calcium, aka 2.4e-4
D = 1            % unitless conversion factor, unclear purpose
% See "Interneuron-mediated inhibition synchronizes neuronal activity during slow
%     oscillation" Chen et al., 2012, Equation 5:
A = 10/(2*96489) % mM cm^2/(ms uA) drive conversion of Ca-modifying currents
CaBufferIC = 0.000001
CaBufferNoiseIC = 0.0001

% ODEs
% In original code, the CaBuffer requires input only from IT_RE
CaBuffer_NRT' = max(-A.*@IT_NRT./D, 0) + (Ca_inf - CaBuffer_NRT)./tauR
CaBuffer_NRT(0) = CaBufferIC+CaBufferNoiseIC.*rand(1,Npop)

% Interface
@CaBuffer_NRT += CaBuffer_NRT
